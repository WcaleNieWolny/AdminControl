plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.4.2'
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
    id("xyz.jpenilla.run-paper") version "1.0.6"
}

group = 'pl.wolny'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = uri("https://papermc.io/repo/repository/maven-public/") }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://nexus.sirblobman.xyz/repository/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        javaParameters = true
    }
}

tasks {
    runServer.dependsOn build
    build.dependsOn shadowJar
}

test {
    useJUnitPlatform()
}

runServer {
    minecraftVersion("1.18.2")
}

shadowJar {
    relocate 'co.aikar.commands', 'pl.wolny.admincontrol.acf'
    relocate 'co.aikar.locales', 'pl.wolny.admincontrol.locales'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-text-minimessage:4.10.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    testImplementation('com.github.seeseemelk:MockBukkit-v1.18:2.22.2')
    testImplementation 'org.slf4j:slf4j-simple:1.8.0-beta4'
    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.mockito:mockito-core:4.6.1')


}



def pluginName = "WolnyAdminControl"

spigot {
    name = pluginName
    authors = ["WcaleNieWolny"]
    version = project.version
    website = "https://github.com/WcaleNieWolny/"
    apiVersion = "1.18"
    excludeLibraries = ['*']
    permissions {
        create("admincontrol.use") {
            description = "Allow usage of /admin command"
            defaults = "op"
        }
        create("admincontrol.chat") {
            description = "Allow to send message when chat is disabled"
            defaults = "op"
        }
    }
}
